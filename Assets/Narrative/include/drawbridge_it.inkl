=== drawbrige ===

= problem
~ x = 0.35
~ y = 0.40
VAR checked_terminal = false
(Clic.) # sfx click
Non possiamo {~procedere|avanzare}.
Potrebbe esserci un fossato, davanti a noi. Molto profondo.
L'edificio fortificato è al di là del fossato. # kidsmove
*   /Uh, una rima!
-> solution

= solution 
*   /Potete calarvi nel fossato?
    Lasciami verificare.
    No, non è possibile.
    Saranno almeno 20 metri, le pareti sono lisce e perfettamente verticali.	
    E in fondo c'è qualcosa… 
	Ragazzi, illuminate là sotto!
	Hmm. Vedo muoversi qualcosa di meccanico e minaccioso.
    * * /OK, direi che le condizioni siano subottimali.
        Decisamente.
    * * /Temo che sia l'unico modo.
        /Mandate giù uno con una corda, con qualcosa…
        Non se ne parla.
        /Non siete così coraggiosi come dite…
        Fottiti.
    - - -> solution
*   /Cos'altro vedete?
    Il parapetto del fossato si interrompe per alcuni metri.
    In quel punto, su una colonnina, c'è… Sembra un monitor.
    Ho mandato un soldato a dare un'occhiata.
    ~ checked_terminal = true
    -> solution
+   /[Esplorate.]{~Esplorate anche|Curiosate anche|Guardatevi un po'|Provate a girargli} attorno.
    Va bene, ci sentiamo tra un po'.
    Passo e chiudo.
    /A tra poco. # shortbreak
    Non abbiamo trovato nulla{\ e le transenne elettrificate impediscono di aggirare il fossato|}.
    {F|Ripeto: f}iniremo troppo lontano dall'obiettivo se non troviamo un modo di entrare nell'edificio fortificato.
    {Abbiamo perso|Ci hai fatto perdere{| altro}} tempo.{|| Ci stiamo innervosendo.}
    -> solution
*   { checked_terminal && Inventory has sphere } /Lanciate la sfera[.] contro il pannello.
    La sfera che abbiamo trovato nella torre?
    /Sì. Ho letto da qualche parte che può servire a sbloccare meccanismi elettronici.
    Mi sembra una teoria del cavolo, ma il tecnico sei tu.
    Pensavo che fosse un estintore.
    ~ Inventory -= sphere
    (Tonfo.) # sfx thump
    È rimbalzata contro il muro.
    Ora la sfera è in fondo al fossato.
    Bella pensata davvero.
    { Events has contamination: /Tanto era contaminata.}
    -> solution
*   { checked_terminal } /Avete controllato il monitor?
    È un terminale.
    Chiede un codice per aprire il varco, ma non capisco a quale varco si riferisca.
    -> code
+   { code } /Tornate al terminale.
    Continua a chiederci un codice…
    -> insert_code
    
= code
{Conosci il codice?|E adesso?}
*   /Non sono sicuro[.], ma proviamoci.
    Sono cinque cifre.
    /Se è lo stesso sistema usato nei bastioni sud, cambia ogni mezz'ora.
    /Quindi non possiamo provarli tutti e centomila.
    Per fortuna: non siamo addestrati a essere molto pazienti.
    -> insert_code
+   { insert_code } /Inseriamo un altro codice.
    -> insert_code
+   /Niente da fare.
    -> solution
 
= insert_code
~ temp code1 = "{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}"
~ temp code2 = "{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}"
~ temp code3 = "{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}{~0|1|2|3|4|5|6|7|8|9|0}"
*   {inter__knows_code} /29979
    -> guessed_code
+   /{ code1 }
    { code1 == "29979": 
        Come hai fatto?! Incredibile. -> guessed_code
    } 
+   /{ code2 }
    { code2 == "29979": 
        Come hai fatto?! Incredibile. -> guessed_code
    } 
+   { not inter__knows_code } /{ code3 }
    { code3 == "29979": 
        Come hai fatto?! Incredibile. -> guessed_code
    }
+   /Nessuno di questi.
    -> insert_code
- No, sbagliato.
-> code

= guessed_code
Dice che è giusto.
/Prego.
Ah!
Un pannello del muro sta ruotando su un perno… #sfx metal
È una specie di ponte levatoio.
-> all_safe

= all_safe
Così possiamo passare. Procediamo.
Passo e chiudo.
/Non è stato necessario provarne centomila, visto?

-> plot