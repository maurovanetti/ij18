=== fire ===

= description
~ x = 0.35
~ y = 0.20
(Click.) # sfx click
Well, well, I suppose we just entered the Ludodrome.
/Beautiful, uh?
It was only to be expected from them.
If you don't force them to here, they want to come anyway.
/However, according to the sensors they didn't pass through there.
They did.
*   /They walked alongside the building.
- No, man. I saw something moving.
They ran through the iron catwalks of the Coalosseum.
My favourite game!
/It's not legal.
Explanations later. Got to move.
Rather, tell me that we have another option to cross this giant hall.
*   /I'm sorry.
    Alright then. <>
*   /You may get out of the Ludodrome[.], since they are not there.
    I told you, I've seen them!
    Let's cross it through the catwalks.
- Scoot upstairs.
{
    - soldiers > 2:
        Not in single file, idiots!
        Each on their own catwalk!
        If you are not exactly told what to do, every time, you are totally inept.
    - soldiers == 2:
        We are only two, we get a catwalk each, Sergeant.
        Swift.
}
/Not all catwalks are going to be bridged, but you may just need to take a longer step, it's not hard.
Not hard, but long and boring.
-> problem

= problem
(Blaze sound.) # sfx fire # intensify
Oh no!
/What's up?
There are flare-ups happening on some catwalks!
*   /Wow! One of the challenges.[] Still working, incredible!
*   /Give up.
    No way. <>
- Now what?
/Only one path is supposed to have fire, isn't it?
Wrong. At least {soldiers} have it.
/Have you got something to put out the fire?
{
    - Inventory has sphere:
        -> walk_with_sphere
    - else:
        -> walk_without_sphere
}

= walk_with_sphere
We have the sphere!
{ Events has contamination: According to you, we shouldn't have because it was contaminated… }
/Excellent. It contains a substance that should extinguish the flames.
/It does everything by itself.
Except that we need to find out who's bringing this sphere.
*   /You keep it.
*   /Give it to the Sergeant.
    I don't think so.
	I'm the commander, I'm keeping it.
- At your go… we run!
*   /Go!
*   /Don't.
    In that case, at mine: go!
- \(Sound of footsteps and blazes.) # sfx fire
(Screams.)
(Gasps.)
OK, we made it.  # abate
We had to bandy the sphere like a volleyball match but we're safe and sound, all {soldiers} of us.
Go get the bastards! Load your weapons!
-> solution

= walk_without_sphere
We should have kept the sphere!
{ Events has contamination: /It was contaminated… }
Alright, we're making an attempt just the same.
Running very fast should be enough.
- At your go… we run!
*   /Go!
*   /Don't.
    In that case, at mine: go!
- \(Sound of footsteps and blazes.)) # sfx fire
(Heartrending screams.)
(Metallic sounds.) # sfx metal
(Silence.) # sfx silence
Not as easy as I believed.
I'm…
Excuse me, I'm shocked.
{
    - soldiers > 3:
        ~ soldiers -= 2
        Keep calm.
        We are still {soldiers}.
        One woman seriously burned.
        /Should I send in a drone?
        Another man plummeted down.
        /Damn.
        He got entangled in the equipment.
		Go get the bastards and then we'll provide for the injured.
        -> solution
    - soldiers == 3:
        ~ soldiers = 1
        I remained alone.
        The uniform… it got caught up.
        The burned flesh… horrific.
        One lost.
        I go get the bastards and then I'll provide for the other, injured.
        /I'm sending a drone.
        -> solution
    - soldiers == 2:
        ~ soldiers = 1
		I remained alone.
		The Sergeant was burned alive. And it's all my fault.
		Out of this cursed place.
        -> plot
}

= solution
Over. # shortbreak
Bad news. # abate
It was just a big rat.
But really big.
/I told you they were not there. Exit from the North-West side.
Roger.
-> plot