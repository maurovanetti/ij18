=== stack ===

= problem
~ x = 0.85
~ y = 1.00
VAR nervous = false
(Clic.) # sfx click
Dove si sono cacciati?! Stiamo girando a vuoto.
Mi sembra di essere già passata di qui.
Questo cos’è? Un cimitero di droni?
*   /Com'è fatto?
    Ci sono centinaia di droni a terra, a distanze regolari.
    Ad alcuni mancano delle eliche. Ad altri il motore.
    /Sì, è proprio un cimitero di droni.
*   /Abbiamo visto qualcosa [del genere…] di simile durante l'ultima ricognizione aerea…
- /La città continua a funzionare da sola.
/Qui è dove accumula i droni da riparare. Ne riutilizza le parti.
/Usa i droni di notte o quando individua migranti clandestini.
Strano: non ne ho ancora visti in volo…
Restiamo concentrati, però.
*   /Vi conviene spostarvi verso sud.
*   /Seguite la via più diretta, a ovest.
    Quel che pensavo di fare.
    A dopo.
    ~ x = 0.80
    ~ y = 1.00
	(Clic.) # sfx click
    Qui Squadra Blu. Abbiamo un problema.
    Una torretta automatica ci tiene d'occhio.
    Ha un mitragliatore. Lo può usare?
    * * /Lo escludo.
        /Procedete con cautela.
        (Spari.) # sfx spari # intensify
        (Voci concitate.)
        ~ Events += turret
        Non sono solita innervosirmi.
        Tuttavia, la tua indicazione era sbagliata.
        ~ nervous = true
        * * *   /Strano! Ero sicuro che non sparasse più.
                Non abbiamo nessun ferito.
                Non ha più importanza.
        * * *   /Perdonatemi, sono stato imprudente.
                Non ha avuto conseguenze.
    * * /Certo! State alla larga.
    - - Allora cosa si fa? # abate
    Ci toccherà tornare indietro e provare a sud.
- Ma li abbiamo visti fuggire a ovest.
/Andate a sud fino alla torre di acciaio rosso.
/Da lì risalite verso ovest.
Ricevuto. Passo e chiudo.
~ x = 0.75
~ y = 1.00
(Clic.) # sfx click
Accidenti. # kidsmove
Stiamo risalendo verso ovest, lungo una sopraelevata.
La strada è bloccata da cataste di oggetti.
/Una barricata?
Qualcosa del genere… È impossibile smuoverli, ci vorrebbe troppo tempo e siamo solo in {soldiers}.
-> solution

= solution
*   (might_climb) /Potreste scalarli.
    { Inventory has rope: -> climb_with_rope } 
    Se avessimo tenuto quei cavi, sì.
    Ma { might_jump: ribadisco che }ci hai detto di lasciarli giù.
    /Oh, è vero.
    Non sono solita innervosirmi.
    Tuttavia, la tua indicazione era {nervous: ancora una volta} sbagliata.
    /Mi dispiace, ma potete scalare anche a mani nude.
    Non mi sembra una grande idea.
    -> solution
+   { might_climb } /Io dico di scalare.
    -> climb_without_rope
*   (might_jump) /Potreste calarvi giù dal viadotto.
    { Inventory has rope: -> jump_with_rope }
    Se avessimo tenuto quei cavi, sì.
    Ma { might_climb: ribadisco che }ci hai detto di lasciarli giù.
    /Ehm…
    Escludiamo l'idea di lanciarci nel vuoto come lemming.
    /Ha senso.
    -> solution
*   /Tornate indietro.
    Capisco il senso del tuo consiglio, ma no.
    Non se ne parla, ci metteremmo troppo tempo.
    Il nostro obiettivo sta avanzando troppo velocemente, rallentare è pericoloso.
    Inoltre non ho visto vie alternative.
    -> solution


= jump_with_rope
Abbiamo quei cavi di acciaio che abbiamo trovato prima.
Possiamo calarci giù in sicurezza.
Mettiamoci all'opera. Passo e chiudo. # longbreak
OK, siamo tornati al livello del terreno.
Proseguiamo fiancheggiando la sopraelevata.
-> plot

= climb_with_rope
Abbiamo quei cavi di acciaio che abbiamo trovato prima.
Possiamo arrampicarci in sicurezza.
Mettiamoci all'opera. Passo e chiudo. # shortbreak
Abbiamo scavalcato la barricata.
Proseguiamo lungo la sopraelevata.
-> plot

= climb_without_rope
Non vedo alternative. Proviamoci.
Spero vada tutto bene. A dopo. # shortbreak
(Lamenti.) # intensify
~ soldiers--
~ Events += fall
No, non è andato tutto bene.
Abbiamo un uomo ferito.
Niente di grave, ma credo si sia rotto una gamba.
/Dannazione!
Dobbiamo lasciarlo indietro, è possibile mandare un drone di soccorso?
*   /Sì.
*   /No.
    Ma come no?! Mandatelo. Per favore.
    /Farò il possibile.
- Conoscete la sua posizione, fate in fretta.
Noi proseguiamo, in {soldiers}.
- /OK.
-> plot



    

        
        
    



-> DONE