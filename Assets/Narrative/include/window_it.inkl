=== window ===

= description
~ x = 0.25
~ y = 1.00
VAR just_checked_corpses = false
(Clic.) # sfx click
Ci era sembrato di rilevare dei movimenti in distanza, ma con questo buio… # kidsmove
*   /Meglio avanzare.
*   /Rallentate.
    Mi sembra un consiglio davvero insensato.
    Piuttosto aiutaci a capire dove siamo.
- C'è un edificio imponente davanti a noi.
*   /Ditemi meglio.
    È un cilindro. Avrà almeno 10 piani.
    Su un'insegna luminosa leggo: «Notiziario della Città Automatizzata: FM 109.0».
    /Ho capito dove siete.
*   /Credo di sapere dove siete.
- La porta è aperta. Entriamo.
-> problem

= problem
*   /Attenti, si[ chiude alle vostre spalle!]…
*   /OK.
*   /Entrate piano.
- \(Schianto metallico.\) # sfx clang # intensify
Cazzo, la porta si è chiusa alle nostre spalle.
L'ingresso si è bloccato!
Contiamoci… 
{soldiers}. Ci siamo tutti. # abate
/Quel passaggio era usato dai trafficanti di migranti.
/Era stato messo in sicurezza.
In effetti in quell'angolo vedo qualcosa che potrebbero essere resti umani.
Resti umani carbonizzati.
Procediamo, sarà meglio. # shortbreak
In una stanza abbiamo trovato una sfera e l'abbiamo presa.
È un guscio metallico semitrasparente, pieno di un gel color ambra.
~ Inventory += sphere
*   /Ma che ve ne fate?
    Si vede che non sei pratico di scenari di emergenza.
    Questo coso funziona come un estintore.
    * *     /[Lasciatelo giù, è contaminato.]No, seriamente, lasciatelo giù.                        
            /Potrebbe essere contaminato.
						/Si facevano… esperimenti pericolosi in questo edificio.
						~ Events += contamination
            Contaminato? e con cosa?
            * * *   /Con… un virus.
                    Mai sentito di virus che attacchino oggetti metallici.
                    /E il tetano?
                    Mi tengo la sfera col morbillo, non ci stressare.
            * * *   /Con… radiazioni.
                    Abbiamo un contatore Geiger e non c'è nessuna radioattività particolare qui.
                    Mi tengo la sfera "radioattiva", non ci stressare.
            * * *   /Ah, fate come vi pare. La vita è la vostra.
                    Va bene, va bene. Se ci tieni tanto, la lasciamo giù.
                    ~ Inventory -= sphere
*   /A-ha.
    Credo sia un estintore.
*   /È una specie di estintore.
    Abbiamo fatto bene a prenderla allora!
- Stiamo controllando le stanze una a una.
A dopo. # longbreak
-> end_of_check

= end_of_check
~ just_checked_corpses = false
Qui Team Rosso.
+   /Rieccovi.
    Q<>
*   /È buio? Riuscite a vedere?
    Perfettamente, ma q<>
+   /Come procede?
    Mah. {|Ci siamo rotti le palle.} Q<>
- ui non c'è nessuno.
Come usciamo?
+   /[Dalla finestra del settimo piano.]Salite al settimo piano, troverete un’unica finestra.
    /Uscite da lì.
    -> solution
+   /Tornate all'ingresso.
    Va bene. # shortbreak
    Siamo tornati all'ingresso.
    Pensa un po': la porta è ancora chiusa.
    + + /Vedete altro?
        In un angolo, ci sono {|que}i {|famosi |cazzo di }resti umani carbonizzati{\ che ti dicevo|}.
        Al centro della stanza, c'è {una specie di|il} tombino triangolare.
        Il tombino è {|sempre }bloccato {da una lunga|dalla|dalla solita} spranga {messa di traverso|di traverso}.
        {Ah! Giurerei di averlo|L'ho di nuovo} visto muoversi.
        * * *   /Muoversi?
                Sì. Come se tentasse di aprirsi, ma la spranga lo tiene fermo.
                /C'è altro?
            Piastrelle rotte, tubi di metallo, detriti… roba così.
        + + +   /OK.
        - -> examine_hall
    + + /Controllate meglio le stanze.
        Mi sembra inutile ma proviamo. # longbreak
        -> end_of_check

= examine_hall
*   /Controllate meglio i resti carbonizzati.
    Che cosa morbosa.
    Non saprei che dire: sono dei tizi morti.
    Sono tutti neri e si sbriciolano.
    A che cazzo serve controllarli?
    /OK, OK.
    Secondo me gli ha sparato qualcuno o qualcosa che sta sotto quel tombino.
    ~ just_checked_corpses = true
    -> examine_hall
*   /Cosa mi dici dei tubi e dei detriti?
    Ti dico che non c'è un bel niente da dire.
    Sicuro di conoscere questo luogo?
    -> examine_hall
+    { not hell_breaks_loose } /Togli la spranga{|, dai}.
    Perché?
    + + /Se avete paura lascia stare.
        {
            - just_checked_corpses:
                {|Ti dico di no. |Ancora?! No! }Abbiamo appena visto quei {cazzo di |}cadaveri!
                Carbonizzati!
                Non {ho intenzione di|voglio} farci sparare addosso.
                -> examine_hall
            - else:
                Paura, noi?
                Non ci conosci.
                Ragazzi, togliete la spranga.
                -> hell_breaks_loose
        } 
        
    * * /Forse c'è un passaggio lì sotto.
        Forse? Non sai di che parli.
        C'è qualcuno di più competente che può sostituirti?
        /Finiscila.
    * * /Le sto provando tutte.
        Non è professionale.
    - - La spranga resta dov'è.
    -> examine_hall
+   /Lasciamo perdere.
    /Tornate dalla parte opposta, oltre le stanze.
    Oh diamine.
    { 
        - hell_breaks_loose:
            Dobbiamo portare la ferita a spalla. # longbreak
        - else:
            A tra poco. # shortbreak
    }
    -> end_of_check
    
= hell_breaks_loose
Non è facile. Ancora un attimo…
(Rumori metallici.) # sfx metal # intensify
(Rumore di mitragliatore.) # sfx gunfire
~ soldiers--
~ Events += turret
(Urla.)
Cazzo cazzo cazzo! # shortbreak
Porca puttana, ma cosa hai fatto?!
C'era una torretta automatica sotto quel "tombino".
Ho una donna ferita. Non possiamo lasciarla qui!
La dobbiamo almeno lasciare all'aperto, per il drone di soccorso.
Perché ci hai fatto togliere la spranga?
/Scusatemi.
Scusatemi il cazzo.
Il "tombino" era la calotta della torretta.
Come facevi a non saperlo?!
Come diamine si esce da qua?
-> examine_hall
        

= solution
Roger. A dopo. # abate
~ x = 0.25
~ y = 0.91
(Clic.) # sfx click
Siamo fuori, su una strada molto ripida.
Il settimo piano dava sulla strada!
*   /Sì[.], la città è in una zona con grandi dislivelli.
- Abbiamo notato.
Che postaccio, perfetto per quella feccia.
/Per chi?
Musi scuri.
Clandestini.
*   /Mio padre[…] ha passato il confine così.
- Ecco perché hai quell'accento di merda.
Sono davvero commosso, guarda.
{
    -   not hell_breaks_loose: 
        -> close_communication
}
Ora cosa facciamo di questa?
L'emorragia si è fermata, chiama il drone.
Non abbiamo neanche una radio da lasciarle.
*   [(Chiama il drone.)] /Chiamato!
*   [(Fai finta.)] /Ehm… chiamato!
- Noi dobbiamo assolutamente proseguire.
-> close_communication


= close_communication
Passo e chiudo.
-> plot