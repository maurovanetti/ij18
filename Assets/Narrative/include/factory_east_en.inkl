=== factory_east ===
~ x = 0.55
~ y = 0.00
(Click.) # sfx click
We reached the food supply factory.
Useless factory, I daresay.
/Indeed, but the automated city didn't realise it.
/The targets are there.
Mission almost accomplished.
*   /You don't believe in luck.
*   /Probably, yes.
- We are so close.
~ inter__blue_in_factory = true
Here is an entrance door.
*   /Then enter.
{
    - soldiers > 2:
        I'm letting the others in from here. I try to catch them from the other door.
    - soldiers == 2:
        Wait.
        I'm letting the other soldier in from here. I try to catch them from the other door.
    - else:
        I'm the only one left, but that should be enough.
}
- I don't want them to escape from the other entrances.
*   /Wise.
{ soldiers > 1: -> get_them }
-> infiltrate


= infiltrate
/You're alone, we need to be clever.
I always am.
I'm in.
{
    - inter__conveyor_belt:
        (Contraptions noise.) # sfx productionline
        The conveyor belt is still working.
        The bedlam is unbearable.
    - else:
        Conveyor belt. Not working.
}
- I realised where they are: in the storage room.
*   /Break into the storage room.
    -> get_them
*   /What else can you see?
    There's a door, the sign says it's the cold room.
    * * /There is a passageway[.] from there, you can take them by surprise.
        -> fridge
    * * /You'd better break in[.]to the storage room.
        Deal.
        -> get_them
= get_them
The threat is in there.
I saw them through a window.
They're feeding on canned fruit.
There's a humungous amount of it.
/Of course, there are no longer human dwellers.
Almost there.
{
    - inter__red_in_factory && not inter__red_in_fridge && not freeze_both:
        Who goes there? # veryshortbreak
        (Voice.)
        Ah! I rejoined Red Team!
        We're breaking in together.
    - else:
        Ready to break in…
}
*   /Roger. # intensify
- \(Squeak of slowly opening door.) # sfx squeak
Hands up!
(Voices.)
(Gunfire.) # sfx bang
(Silence.) # sfx silence
Face the wall!
*   /What happened?
- One down. I took out one target.
The deceased, male, is of foreign race, apparent age 18 years.
The other captives are one male and two females, foreign race.
Apparent age 14, 18, 24.
*   /14?
*   /14!
*   /14?!
- Yes. Proceeding to execution.
/What?!
(Gunshot.) # sfx bang
(Gunshot.) # sfx bang
(Gunshot.) # sfx bang
*   /…
- For the record: 4 illegal migrant units executed according to the State Security Act no. 103/2079. # abate
Mission accomplished.
Team Blue casualties: {5 - soldiers}.
Enemy casualties: 4.
*   [R E P L A Y] GAME OVER # restart
-> END

= fridge
{
    - inter__fridge_locked && inter__red_in_fridge:
        The door of the cold room is locked.
        There is someone in the room.
        That man… is Red Team's commander!
        /That's why he wasn't communicating!
        Non-optimal situation.
        I can only break into the storage room now.
        +   /Wait…
            No more time. We'll deal with him later.
            -> get_them
        +   /Hell no!
            Hell yeah. We'll deal with him later.
            -> get_them
    - inter__fridge_locked && not inter__red_in_fridge:
        {T|As I already told you, t}he door of the cold room is locked.
        I can only break into the storage room now.
        +   /Wait… Try now.
            -> fridge
        +   /OK!
            -> get_them
    - else:
        The door is open.
        ~ inter__blue_in_fridge = true
}
Stink of rotting meat.
*   /Robots don't eat.
-> rendez_vous

= rendez_vous
{
    - inter__fridge_locked:
        Temperature is falling quickly!        
        +   /Muahahaha!
            Why you laughing?
            -> freeze_one
        +   /Oops!
            -> freeze_one
    - inter__blue_in_fridge:
        Who goes there?
        (Voice.)
        Ah! I rejoined Red Team!
        I'm puzzled that you're here as well.
        (Voice.)
        +   /Oh, what a coincidence!
            -> both_in_fridge
        +   /Say your prayers!
            What??
            -> both_in_fridge
    - else:
        -> wait_in_fridge
}

= wait_in_fridge
I can't understand what I'm supposed to do.
I see no passageway to the storage room.
{There's another door to get out from the opposite end, but I doubt it's helpful|We are wasting time}.
+   /Wait.
    -> rendez_vous
+   /Come back and break in[.]to the storage room.
    We shouldn't be wasting our time like this.
    I walked back and now I'm moving toward the door that leads to the storage room.
    -> get_them

= freeze_one
(Gasps.) # intensify
(Silence.)
/I suppose that Blue Team won't be pursuing migrants anymore. # abate
/Here's to you, dad. # reddeath
-> END

= both_in_fridge
{ inter__fridge_locked: -> freeze_both }
{&What does {&|all} this mean?|Uh?|What are you talking about?}
+   /I'm going to show you.
    -> both_in_fridge
+   /You guys really have no sense of humour, have you?
    I'm speechless.
    Let's go to the storage room together.
    -> get_them

= freeze_both
(Screams.) # intensify
(Punching on doors.) # sfx bam
(Gasps.)
/Bye bye, Read Team. # reddeath
(Silence.) # sfx silence
/Bye bye, Blue Team. # bluedeath
/I suppose your mission failed. # abate
/Here's to you, dad.
(Loudspeaker: «ATTENTION ATTENTION.») # victory
VAR escape_meters = 3000

-> victory

= victory
~ escape_meters -= 250
(«ILLEGAL MIGRANTS FLEEING FROM NORTH-NORTH-WEST SIDE».)
(«YOU HAVE BEEN RECOGNISED: XERO PAONONGA, 18. STOP!»)
(«YOU HAVE BEEN RECOGNISED: KALIFA OKEKE DIYA, 18. STOP!»)
(«YOU HAVE BEEN RECOGNISED: RACHIDA NANSUBUGA, 24. STOP!»)
(«YOU HAVE BEEN RECOGNISED: KANTIGI OKEKE DIYA, 14. STOP!»)
(«THE FUGITIVES ARE LOCATED AT {escape_meters} METRES FROM THE BORDER.») # shortbreak
{ escape_meters < 0:
    («THE FUGITIVES HAVE TRESPASSED THE BORDER.»)
    -> END
}
-> victory


