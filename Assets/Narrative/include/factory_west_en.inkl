=== factory_west ===
~ x = 0.45
~ y = 0.00
(Clic.) # sfx click
We reached a massive warehouse or something.
/Has it got a smokestack?
Let me check…
Yes.
/Then it's the food supply factory.
/They're in there.
What's it for? Nobody lives here anymore.
/No shit, but the city automation didn't get it.
This way it will end up feeding the porch monkeys on the run.
*   /They must still eat too.
*   /Moochers.
- The fence was torn off here.
~ inter__red_in_factory = true
There's a sign: «Don't trespass. Identify on FM 102.5».
Uh, I found an entrance.
*   /Enter.
{
    - soldiers > 2:
        Wait.
        I'm letting the others enter from here, while I try to catch them from behind.
    - soldiers == 2:
        Wait.
        I'm letting the other soldier enter from here, while I try to catch them from behind.
    - else:
        I'm the only one left, but that should be enough.
}
- I don't want them to escape from the other entrances.
*   /Wise.
{ soldiers > 1: -> get_them }
-> infiltrate


= infiltrate
/You're alone, you've got to move silently.
Thanks for your useless advice.
(Squeak.) # sfx squeak
/Be careful not to make any noise.
{
    - inter__conveyor_belt:
        (Contraptions noise.) # sfx productionline
        There's no risk, the conveyor belt is still working.
        Such a ruckus!
    - else:
        Shut up.
        I'm at the conveyor belt here. It's offline, of course.
}
- I realised where they are: in the storage room.
*   /Break into the storage room.
    -> get_them
*   /What else can you see?
    A door, and a sign that says it's the cold room.
    * * /Go there, there's a passageway[.] and you can take them by surprise.
        -> fridge
    * * /You'd better break in[.]to the storage room.
        Yeah.
        -> get_them
= get_them
I peek through a window.
The threat is in there.
They ransacked the canned fruit.
Getting food poisoning, hopefully.
OK, there we are.
{
    - inter__blue_in_factory && not inter__blue_in_fridge && not freeze_both:
        Hey! Who goes there? # veryshortbreak
        (Voice.)
        Ah! I rejoined Blue Team!
        Going to break in together.
    - else:
        Ready to break in…
}
*   /Roger.
- \(Sound of door being kicked open.) # intensify # lock
Hands up!
(Voices.)
(Gunfire.) # sfx bang
(Silence.) # sfx silence
Face the wall!
Leave your little friend alone!
You'll be visiting him in porch monkeys' heaven.
*   /What happened?
- Suspicious movement, I had to fire. One deceased.
The casualty is male, of foreign race, apparent age 18.
The other prisoners are same race, two females and one male.
Apparent age 18, 24, 14.
*   /14?
*   /14!
*   /14?!
- Yes. Proceeding to execution.
/What?!
(Gunshot.) # sfx bang
(Gunshot.) # sfx bang
(Gunshot.) # sfx bang
*   /…
- For the record: Thartwed attempt at illegal immigration. # abate
Mission accomplished.
Red Team casualties: {5 - soldiers}.
Enemy casualties: 4.
*   [R E P L A Y] GAME OVER # restart
-> END

= fridge
{
    - inter__fridge_locked && inter__blue_in_fridge:
        The door of the cold room is locked.
        Hey! But there' someone here.
        She… is Blue Team's commander!        
        /That's why she wasn't communicating!
        Damn.
        I can only break into the storage room now.
        +   /Wait…
            No more time. We'll deal with her later.
            -> get_them
        +   /Hell no!
            Hell yeah. We'll deal with her later.
            -> get_them
    - inter__fridge_locked && not inter__blue_in_fridge:
        {T|As I already told you, t}he door of the cold room is locked.
        I can only break into the storage room now.
        +   /Wait… Try now.
            -> fridge
        +   /OK!
            -> get_them
    - else:
        The door is open.
        ~ inter__red_in_fridge = true
}
Yuck! What a deadly rancid smell!
*   /The meat rotted[.], but robots don't mind.
-> rendez_vous

= rendez_vous
{
    - inter__fridge_locked:
        Hey! It's getting supercold…
        +   /Muahahaha!
            Why. You. Laughing?
            -> freeze_one
        +   /Oops!
            -> freeze_one
    - inter__blue_in_fridge:
        Hey! Who goes there?
        (Voice.)
        Ah! I rejoined Blue Team!
        What are you doing here too?
        (Voice.)
        +   /Oh, what a coincidence!
            -> both_in_fridge
        +   /Say your prayers!
            What??
            -> both_in_fridge
    - else:
        -> wait_in_fridge
}

= wait_in_fridge
What should I do now?
I can't see no passageway to the storage room.
{There's another door to get out from the opposite end, though|We are wasting time}.
+   /Wait.
    -> rendez_vous
+   /Come back and break in[.]to the storage room.
    I still enjoyed the fridge tour.
    I wish it was cooler.
    I walked back and now I'm moving toward the door leading to the storage room.
    -> get_them

= freeze_one
(Gasps.) # intensify
(Silence.) # sfx silence
/I suppose that Red Team won't be pursuing migrants anymore. # abate
/Here's to you, dad. # bluedeath
-> END

= both_in_fridge
{ inter__fridge_locked: -> freeze_both }
{&What's {&|all} this supposed to mean?|Uh?|What are you saying?}
+   /I'm going to show you.
    -> both_in_fridge
+   /You guys really have no sense of humour[.], have you?
    Screw you.
    Let's go to the storage room together.
    -> get_them

= freeze_both
(Screams.) # intensify # lock
(Punching on doors.) # sfx bam
(Gasps.)
/Bye bye, Read Team. # reddeath
(Silence.) # sfx silence
/Bye bye, Blue Team. # bluedeath
/I suppose your mission failed. # abate
/Here's to you, dad.
(Loudspeaker: «ATTENTION ATTENTION.») # victory
VAR escape_meters = 3000

-> victory

= victory
~ escape_meters -= 250
(«ILLEGAL MIGRANTS FLEEING FROM NORTH-NORTH-WEST SIDE».)
(«YOU HAVE BEEN RECOGNISED: XERO PAONONGA, 18. STOP!»)
(«YOU HAVE BEEN RECOGNISED: KALIFA OKEKE DIYA, 18. STOP!»)
(«YOU HAVE BEEN RECOGNISED: RACHIDA NANSUBUGA, 24. STOP!»)
(«YOU HAVE BEEN RECOGNISED: KANTIGI OKEKE DIYA, 14. STOP!»)
(«THE FUGITIVES ARE LOCATED AT {escape_meters} METRES FROM THE BORDER.») # shortbreak
{ escape_meters < 0:
    («THE FUGITIVES HAVE TRESPASSED THE BORDER.»)
    -> END
}
-> victory


